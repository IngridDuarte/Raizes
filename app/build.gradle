buildscript {
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'

sourceCompatibility = 11
targetCompatibility = 11

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-starter-data-jpa:${springBootVersion}"
    compile "org.springframework.boot:spring-boot-devtools:${springBootVersion}"
    compile "org.flywaydb:flyway-core"
    compile "com.h2database:h2"
    compile "org.postgresql:postgresql:42.2.5"

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:2.21.0"
}

repositories {
    mavenCentral()
}

task startFromJar(type: Exec, dependsOn: assemble) {
    description = "Inicia aplicacao em plano de fundo, utilizando o jar gerado pela task :app:assemble"

    commandLine "${rootDir}/scripts/local-test-server", "start", "$buildDir/libs/app.jar", "$buildDir"
}

task stopRunningJar(type: Exec) {
    description = "Para aplicacao que foi iniciada por :app:startFromJar"

    commandLine "${rootDir}/scripts/local-test-server", "stop", "$buildDir/libs/app.jar", "$buildDir"
}

bootRun {
  def activeProfiles = 'dev'

  jvmArgs = [
    "-Dspring.profiles.active=$activeProfiles"
  ]
}

build.mustRunAfter('clean')
